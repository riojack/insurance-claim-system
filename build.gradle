plugins {
    id 'com.adarshr.test-logger' version '2.1.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.adarshr.test-logger'

    repositories {
        mavenCentral()
    }

    sourceSets {
        acceptance {
            java.srcDirs = ['src/acceptance/java']
            resources.srcDirs = ['src/acceptance/resources']
        }
    }

    test {
        dependsOn 'cleanTest'
        testlogger {
            theme 'standard'
        }
    }

    task acceptance(type: Test) {
        group = LifecycleBasePlugin.VERIFICATION_GROUP
        description = 'Runs acceptance tests.'
        testClassesDirs = sourceSets.acceptance.output.classesDirs
        classpath = sourceSets.acceptance.runtimeClasspath
    }

    task fatJar(type: Jar) {
        manifest {
            attributes 'Main-Class': 'com.insu.claim.Application'
        }
        archiveBaseName = 'insurance-claim-system'
        destinationDirectory = file('build/jars')
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }

    dependencies {
        implementation 'joda-time:joda-time:2.10'

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.hamcrest:hamcrest-library:1.3'

        acceptanceImplementation 'io.cucumber:cucumber-java8:3.0.2'
        acceptanceImplementation 'io.cucumber:cucumber-junit:3.0.2'
        acceptanceImplementation 'org.assertj:assertj-core:3.10.0'
    }
}
